jQuery ->
  Stripe.setPublishableKey("<%= ENV['STRIPE_PUBLIC_KEY'] %>")
  payment.setupForm()

payment =
  setupForm: ->
    $('#new_order').submit ->
      $('input[type=submit]').attr('disabled', true)
      if $('#card_number').length
        payment.processCard()
        false
      else
        true

  processCard: ->
    card =
      name: $('#order_first_name').val() + ' ' + $('#order_last_name').val()
      address_line1: $('#order_address').val()
      address_city: $('#order_city').val()
      address_state: $('#order_state').val()
      address_zip: $('#order_zipcode').val()
      number: $('#card_number').val()
      cvc: $('#card_code').val()
      exp_month: $('#card_month').val()
      exp_year: $('#card_year').val()
    Stripe.createToken(card, payment.handleStripeResponse)

  handleStripeResponse: (status, response) ->
    if status == 200
      $('#order_stripe_card_token').val(response.id)
      $('#new_order')[0].submit()
    else
      $('#stripe_error').text(response.error.message)
      $('input[type=submit]').attr('disabled', false)